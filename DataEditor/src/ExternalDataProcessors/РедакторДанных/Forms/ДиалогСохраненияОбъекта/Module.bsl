///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2026, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Параметры.Свойство("ЗаписьВРежимеОбмена", ЗаписьВРежимеОбмена);
	Параметры.Свойство("ВыбранныйРежимЗаписиДокумента", ВыбранныйРежимЗаписиДокумента);

	Если Параметры.Свойство("ДополнительныеСвойства") Тогда
		//@skip-check unknown-form-parameter-access
		Для Каждого СтрокаСвойств Из Параметры.ДополнительныеСвойства Цикл
			НовоеСвойство = ДополнительныеСвойства.Добавить();
			НовоеСвойство.Ключ = СтрокаСвойств.Ключ;
			НовоеСвойство.Значение = СтрокаСвойств.Значение;
			НовоеСвойство.Пометка = СтрокаСвойств.Пометка;
		КонецЦикла;
	КонецЕсли;

	//@skip-check unknown-form-parameter-access
	Если Параметры.Свойство("ЭтоДокумент") И Параметры.ЭтоДокумент Тогда
		Элементы.ВыбранныйРежимЗаписиДокумента.Видимость = Истина;
	Иначе
		Элементы.ВыбранныйРежимЗаписиДокумента.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьДоступностьЗаписиВРежимеОбмена();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыбранныйРежимЗаписиДокументаПриИзменении(Элемент)

	УстановитьДоступностьЗаписиВРежимеОбмена();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДополнительныеСвойства

&НаКлиенте
Процедура ДополнительныеСвойстваПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)

	Попытка
		Результат = Новый Массив();

		Для Каждого СтрокаСвойств Из ДополнительныеСвойства Цикл
			НовоеСвойство = Новый Структура(СтрокаСвойств.Ключ, СтрокаСвойств.Значение);
			Результат.Добавить(НовоеСвойство);
		КонецЦикла;

	Исключение
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Введенные данные невозможно упаковать в структуру'");
		Сообщение.Поле = "ДополнительныеСвойства";
		Сообщение.Сообщить();
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьВсеФлажки(Команда)

	Для Каждого СтрокаПараметров Из ДополнительныеСвойства Цикл
		СтрокаПараметров.Пометка = Истина;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеФлажки(Команда)

	Для Каждого СтрокаПараметров Из ДополнительныеСвойства Цикл
		СтрокаПараметров.Пометка = Ложь;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СохранитьОбъект(Команда)

	УпакованныеСвойства = УпаковатьДополнительныеСвойства();
	Если УпакованныеСвойства = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПараметрыЗакрытия = Новый Структура();
	ПараметрыЗакрытия.Вставить("ЗаписьВРежимеОбмена", ЗаписьВРежимеОбмена);
	ПараметрыЗакрытия.Вставить("ВыбранныйРежимЗаписиДокумента", ВыбранныйРежимЗаписиДокумента);
	ПараметрыЗакрытия.Вставить("ДополнительныеСвойства", УпакованныеСвойства);
	ПараметрыЗакрытия.Вставить("Сохранить");

	Закрыть(ПараметрыЗакрытия);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция УпаковатьДополнительныеСвойства()

	Результат = Новый Массив();
	ЕстьОшибки = Ложь;

	Для ИндексСтроки = 0 По ДополнительныеСвойства.Количество() - 1 Цикл
		Если ПустаяСтрока(ДополнительныеСвойства[ИндексСтроки].Ключ) Тогда
			ЕстьОшибки = Истина;

			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Не заполнено поле ""Ключ""'");
			Сообщение.Поле = СтрШаблон("ДополнительныеСвойства[%1].Ключ", ИндексСтроки);
			Сообщение.Сообщить();
			Продолжить;
		КонецЕсли;

		НовоеСвойство = Новый Структура();
		НовоеСвойство.Вставить("Ключ", ДополнительныеСвойства[ИндексСтроки].Ключ);
		НовоеСвойство.Вставить("Значение", ДополнительныеСвойства[ИндексСтроки].Значение);
		НовоеСвойство.Вставить("Пометка", ДополнительныеСвойства[ИндексСтроки].Пометка);
		Результат.Добавить(НовоеСвойство);
	КонецЦикла;

	Если ЕстьОшибки Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьЗаписиВРежимеОбмена()

	Если ВыбранныйРежимЗаписиДокумента = "Проведение" Или ВыбранныйРежимЗаписиДокумента = "ОтменаПроведения" Тогда
		// BSLLS:UnusedLocalVariable-off
		ЗаписьВРежимеОбмена = Ложь;
		// BSLLS:UnusedLocalVariable-on
		Элементы.ЗаписьВРежимеОбмена.Доступность = Ложь;

	Иначе
		Элементы.ЗаписьВРежимеОбмена.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти