///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2026, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	КоллекцияПризнаков = Новый Структура();
	КоллекцияПризнаков.Вставить("Выключен", 0);
	КоллекцияПризнаков.Вставить("Включен", 1);
	// BSLLS:MagicNumber-off
	КоллекцияПризнаков.Вставить("НеОпределено", 2);
	// BSLLS:MagicNumber-on

	Настройки = ХранилищеОбщихНастроек.Загрузить("РедакторДанных", "НастройкиОтбораПодсистем");

	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОтрисовкаДереваЗначений(Настройки, ОбработкаОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;

	СохранитьНастройки();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодсистемыКонфигурации

&НаКлиенте
Процедура ПодсистемыКонфигурацииОтборПриИзменении(Элемент)

	ТекущаяСтрокаДерева = ТекущийЭлемент.ТекущиеДанные;

	Если ТекущаяСтрокаДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ТекущаяСтрокаДерева.ПолучитьРодителя() = Неопределено Тогда
		ОпределениеТекущегоСостоянияСтроки(ТекущаяСтрокаДерева.Отбор);
		ОбходПодчиненныхСтрокПодсистемыКонфигурации(ТекущаяСтрокаДерева.ПолучитьЭлементы(), ТекущаяСтрокаДерева.Отбор);
		Возврат;
	КонецЕсли;

	ОпределениеТекущегоСостоянияСтроки(ТекущаяСтрокаДерева.Отбор);

	ОбходПодчиненныхСтрокПодсистемыКонфигурации(ТекущаяСтрокаДерева.ПолучитьЭлементы(), КоллекцияПризнаков.НеОпределено);
	ОбходРодительскихСтрокПодсистемыКонфигурации(ТекущаяСтрокаДерева.ПолучитьРодителя(), КоллекцияПризнаков.НеОпределено);

	УстановитьЗначениеКорневогоУзла(ПодсистемыКонфигурации.ПолучитьЭлементы(), Ложь,
		ТекущаяСтрокаДерева.Отбор = КоллекцияПризнаков.Выключен);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)

	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура Установить(Команда)

	УстановитьЗначениеКорневогоУзла(ПодсистемыКонфигурации.ПолучитьЭлементы(), Ложь, Ложь);
	ОповеститьОВыборе(СписокПодсистем);

КонецПроцедуры

&НаКлиенте
Процедура Отключить(Команда)

	УстановитьЗначениеКорневогоУзла(ПодсистемыКонфигурации.ПолучитьЭлементы(), Истина, Ложь);
	ОповеститьОВыборе(СписокПодсистем);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройки()

	КорневойУзелКонфигурации = ПодсистемыКонфигурации.ПолучитьЭлементы()[0];

	Отбор = Новый Соответствие();
	Отбор.Вставить(КорневойУзелКонфигурации.ПолноеИмяОбъектаМетаданных, КорневойУзелКонфигурации.Отбор);

	ПодчиненныйУзелКонфигурации = КорневойУзелКонфигурации.ПолучитьЭлементы();

	ОбходДереваФормы(ПодчиненныйУзелКонфигурации, Отбор);

	ХранилищеОбщихНастроек.Сохранить("РедакторДанных", "НастройкиОтбораПодсистем", Отбор);

КонецПроцедуры

&НаСервере
Процедура ОбходДереваФормы(ПодчиненныеЭлементыДерева, Отбор)

	Для Каждого ТекущийЭлементДерева Из ПодчиненныеЭлементыДерева Цикл
		Отбор.Вставить(ТекущийЭлементДерева.ПолноеИмяОбъектаМетаданных, ТекущийЭлементДерева.Отбор);
		ПодчиненныеЭлементыДерева = ТекущийЭлементДерева.ПолучитьЭлементы();
		ОбходДереваФормы(ПодчиненныеЭлементыДерева, Отбор);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбходРодительскихСтрокПодсистемыКонфигурации(ТекущаяСтрокаДерева, Отбор)

	Если ТекущаяСтрокаДерева.ПолучитьРодителя() = Неопределено Тогда
		ТекущаяСтрокаДерева.Отбор = КоллекцияПризнаков.НеОпределено;
		Возврат;
	КонецЕсли;

	ТекущаяСтрокаДерева.Отбор = Отбор;
	Если ВключатьОбъектыРодительскихПодсистем Тогда
		СписокПодсистем.Добавить(ТекущаяСтрокаДерева.ПолноеИмяОбъектаМетаданных);
	КонецЕсли;
	ОбходРодительскихСтрокПодсистемыКонфигурации(ТекущаяСтрокаДерева.ПолучитьРодителя(), Отбор);

КонецПроцедуры

&НаКлиенте
Процедура ОбходПодчиненныхСтрокПодсистемыКонфигурации(ТекущаяСтрокаДерева, Отбор)

	Для Каждого ЭлементСтроки Из ТекущаяСтрокаДерева Цикл
		ЭлементСтроки.Отбор = Отбор;
		Если ВключатьОбъектыПодчиненныхПодсистем Тогда
			СписокПодсистем.Добавить(ЭлементСтроки.ПолноеИмяОбъектаМетаданных);
		КонецЕсли;
		ОбходПодчиненныхСтрокПодсистемыКонфигурации(ЭлементСтроки.ПолучитьЭлементы(), ЭлементСтроки.Отбор);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеКорневогоУзла(ТекущаяСтрокаДерева, Отключить, СнятьУстановкуСКорня)

	Если Отключить Тогда
		ТекущаяСтрокаДерева[0].Отбор = КоллекцияПризнаков.Включен;
	ИначеЕсли СнятьУстановкуСКорня Тогда
		ТекущаяСтрокаДерева[0].Отбор = КоллекцияПризнаков.Выключен;
	КонецЕсли;

	СписокПодсистем.Очистить();

	ОбходПодсистемыКонфигурации(ТекущаяСтрокаДерева[0].ПолучитьЭлементы(), Отключить,
		ТекущаяСтрокаДерева[0].Отбор = КоллекцияПризнаков.Включен);

КонецПроцедуры

&НаКлиенте
Процедура ОбходПодсистемыКонфигурации(ПодчиненныеЭлементы, Отключить, КореньВыбран)

	Для Каждого ЭлементСтроки Из ПодчиненныеЭлементы Цикл

		Если Отключить Или КореньВыбран Тогда
			УстановкаОтбораСтроки(ЭлементСтроки, КоллекцияПризнаков.НеОпределено, Отключить, КореньВыбран);
			Продолжить;
		КонецЕсли;

		Если ЭлементСтроки.Отбор = КоллекцияПризнаков.Включен Тогда
			СписокПодсистем.Добавить(ЭлементСтроки.ПолноеИмяОбъектаМетаданных);
			ОбходПодчиненныхСтрокПодсистемыКонфигурации(ЭлементСтроки.ПолучитьЭлементы(), КоллекцияПризнаков.НеОпределено);
			ОбходРодительскихСтрокПодсистемыКонфигурации(ЭлементСтроки.ПолучитьРодителя(), КоллекцияПризнаков.НеОпределено);
			Продолжить;
		КонецЕсли;

		УстановкаОтбораСтроки(ЭлементСтроки, КоллекцияПризнаков.Выключен, Отключить, КореньВыбран);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура УстановкаОтбораСтроки(ТекущаяСтрока, Отбор, Отключить, КореньВыбран)

	ТекущаяСтрока.Отбор = Отбор;
	ОбходПодсистемыКонфигурации(ТекущаяСтрока.ПолучитьЭлементы(), Отключить, КореньВыбран);

КонецПроцедуры

&НаКлиенте
Процедура ОпределениеТекущегоСостоянияСтроки(Отбор)

	Если Отбор = КоллекцияПризнаков.Выключен Тогда
		Отбор = КоллекцияПризнаков.Включен;
	ИначеЕсли Отбор = КоллекцияПризнаков.НеОпределено Тогда
		Отбор = КоллекцияПризнаков.Выключен;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция УстановитьОтбор(ПоДаннымИзНастроек, ПолноеИмяОбъектаМетаданных, ГлавныйУзел)

	Если Не ПоДаннымИзНастроек = Неопределено И Не ПоДаннымИзНастроек[ПолноеИмяОбъектаМетаданных] = Неопределено Тогда
		Возврат ПоДаннымИзНастроек[ПолноеИмяОбъектаМетаданных];
	КонецЕсли;

	Возврат ?(ГлавныйУзел, КоллекцияПризнаков.Включен, КоллекцияПризнаков.НеОпределено);

КонецФункции

&НаСервере
Процедура ОбходИерархииПодсистемКонфигурации(Подсистема, Узел, ПоДаннымИзНастроек, ОбработкаОбъект)

	Узел.Подсистема = Подсистема.Имя;
	Узел.Картинка = Новый Картинка(ОбработкаОбъект.ПолучитьМакет("ПодсистемаКартинка"));
	Узел.ПолноеИмяОбъектаМетаданных = Подсистема.ПолноеИмя();

	Узел.Отбор = УстановитьОтбор(ПоДаннымИзНастроек, Узел.ПолноеИмяОбъектаМетаданных, Ложь);

	ПодчиненныеПодсистемы = Подсистема.Подсистемы;

	Для Каждого ПодчиненнаяПодсистема Из ПодчиненныеПодсистемы Цикл
		ПодчиненныеУзлы = Узел.ПолучитьЭлементы().Добавить();
		ОбходИерархииПодсистемКонфигурации(ПодчиненнаяПодсистема, ПодчиненныеУзлы, ПоДаннымИзНастроек, ОбработкаОбъект);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОтрисовкаДереваЗначений(ПоДаннымИзНастроек, ОбработкаОбъект)

	ГлавныйУзел = ПодсистемыКонфигурации.ПолучитьЭлементы().Добавить();

	ГлавныйУзел.Подсистема = Метаданные.Имя;
	ГлавныйУзел.Картинка = Новый Картинка(ОбработкаОбъект.ПолучитьМакет("КонфигурацияКартинка"));

	ГлавныйУзел.ПолноеИмяОбъектаМетаданных = Метаданные.ПолноеИмя();

	ГлавныйУзел.Отбор = УстановитьОтбор(ПоДаннымИзНастроек, ГлавныйУзел.ПолноеИмяОбъектаМетаданных, Истина);

	Для Каждого Подсистема Из Метаданные.Подсистемы Цикл
		ПодчиненныеУзлы = ГлавныйУзел.ПолучитьЭлементы().Добавить();
		ОбходИерархииПодсистемКонфигурации(Подсистема, ПодчиненныеУзлы, ПоДаннымИзНастроек, ОбработкаОбъект);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
